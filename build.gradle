import java.nio.charset.StandardCharsets
import java.time.Duration

plugins {
    /* Beekeeper */
    id 'io.beekeeper.gradle.plugins.dependency-updates' version '0.9.14' apply false
    id 'io.beekeeper.gradle.plugins.formatter' version '0.9.14' apply false
    id 'io.beekeeper.gradle.plugins.ide' version '0.9.14' apply false
    /* Publishing */
    id 'de.marcphilipp.nexus-publish' version '0.4.0'
    /* General */
    id 'base'
    id 'checkstyle'
}

allprojects {
    group 'com.heretere.hch'
    version '1.0.0'
    description "Runtime configuration generation"

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

    [JavaCompile, Javadoc].forEach { taskType ->
        tasks.withType(taskType) {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
            options.encoding(StandardCharsets.UTF_8.name())
        }
    }
}

subprojects { subproject ->
    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'de.marcphilipp.nexus-publish'
    apply plugin: 'io.beekeeper.gradle.plugins.dependency-updates'
    apply plugin: 'io.beekeeper.gradle.plugins.formatter'
    apply plugin: 'io.beekeeper.gradle.plugins.ide'

    repositories {
        mavenCentral()
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    nexusPublishing {
        connectTimeout.set(Duration.ofMinutes(10))
        clientTimeout.set(Duration.ofMinutes(10))
        repositories {
            sonatype()
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    name = "Heretere's Config Handler"
                    artifactId = subproject.name
                    packaging = 'jar'
                    url = 'https://github.com/heretere/hch'
                    description = subproject.description

                    developers {
                        developer {
                            id = 'Heretere'
                            name = 'Justin Heflin'
                            url = 'https://heretere.com'
                            email = 'heirteir@protonmail.com'
                        }
                    }

                    issueManagement {
                        system = 'GitHub Issues'
                        url = 'https://github.com/heretere/hch/issues'
                    }

                    scm {
                        connection = 'scm:git@github.com:heretere/hch.git'
                        developerConnection = 'scm:git@github.com:heretere/hch.git'
                        url = 'https://github.com/heretere/hch'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                }
            }
        }

        signing {
            useGpgCmd()
            sign publishing.publications.mavenJava
        }
    }

    javadoc {
        title = subproject.name
        source subproject.sourceSets.main.allJava
        classpath = subproject.sourceSets.main.compileClasspath
        options {
            links 'https://docs.oracle.com/en/java/javase/11/docs/api',
                    'https://javadoc.io/doc/org.jetbrains/annotations/20.1.0',
                    'https://hub.spigotmc.org/javadocs/spigot/'
        }
    }
}
